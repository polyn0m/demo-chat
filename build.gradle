plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.6.20'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

group 'com.example.demo-chat'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    libOkHttp = '4.9.3'
    libRetrofit = '2.9.0'

    junitVersion = '5.8.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.example.demochat'
    mainClass = 'com.example.demochat.DemoChat'
}

[compileKotlin, compileTestKotlin].forEach {
    it.kotlinOptions {
        jvmTarget = '11'
    }
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    // okhttp
    implementation("com.squareup.okhttp3:okhttp:${libOkHttp}")
    implementation("com.squareup.okhttp3:logging-interceptor:${libOkHttp}")
    implementation("com.squareup.okhttp3:okhttp-urlconnection:${libOkHttp}")

    // retrofit
    implementation("com.squareup.retrofit2:retrofit:${libRetrofit}")
    implementation("com.squareup.retrofit2:converter-gson:${libRetrofit}")

}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}